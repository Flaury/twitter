<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" viewSourceURL="srcview/index.html" applicationComplete="init();">
	<mx:Script>
        <![CDATA[
            import mx.controls.Alert;
            import com.swfjunkie.tweetr.Tweetr;
            import com.swfjunkie.tweetr.events.TweetEvent;
            import com.swfjunkie.tweetr.data.objects.StatusData;
        
        	/**
        	 * A Simple example of how to use the Tweetr API
	 		 * @author Sandro Ducceschi [swfjunkie.com, Switzerland]
	 		 */
	 		 
        	public function init():void
        	{
        	    // create a new tweetr instance
            	var tweetr:Tweetr = new Tweetr();
            	// since we are using it on the web, set 
            	// the serviceHost to the tweetr php-proxy
            	tweetr.serviceHost = "labs.swfjunkie.com/tweetr/proxy";
            	
            	// set ghost authentication
            	tweetr.username = "ghost";
            	tweetr.password = "ghost";
                
                // add event listeners
            	tweetr.addEventListener(TweetEvent.COMPLETE, handleLoad);
            	tweetr.addEventListener(TweetEvent.FAILED, handleFail);
            	
            	// now let's retrieve the friends timeline
            	tweetr.getFriendsTimeLine();
        	}
        	
        	/**
        	 * @private
        	 * If everything goes well, we get an event with an
        	 * array full of StatusData Objects
        	 */
        	private function handleLoad(event:TweetEvent):void
        	{
        	    // we'll create a simple array with a object
        	    // containing the tweet and who sent it.
        	    var arr:Array = [];
        	    for(var i:int = 0; i < event.responseArray.length; i++)
        	    {
        	        var statusData:StatusData = event.responseArray[i];
        	        var obj:Object = {};
        	        obj.text =  statusData.text;
        	        obj.from =  statusData.user.screenName;
        	        arr.push(obj);
        	    }
        	    // then pass it to the datagrid
        	    datagrid.dataProvider = arr;
        	}
        	
        	/**
        	 * @private
        	 * If something go wrong, show an alert window with the info
        	 */ 
        	private function handleFail(event:TweetEvent):void
        	{
        	    Alert.show(event.info,"Epic Fail");
        	}
        ]]>
    </mx:Script>
    
    
    <mx:Panel title="Tweets from @_sandro and his friends Timeline" width="100%">
	<mx:DataGrid id="datagrid" width="100%" minHeight="400">
		<mx:columns>
			<mx:DataGridColumn headerText="Tweet" dataField="text"/>
			<mx:DataGridColumn headerText="@user" width="150" dataField="from"/>
		</mx:columns>
	</mx:DataGrid>
	</mx:Panel>    
</mx:Application>